# Nix
if [ ! -f "/etc/NIXOS" ]; then
  # Determinate Systems installer path
  if [ -f "/nix/var/nix/profiles/default/etc/profile.d/nix-daemon.sh" ]; then
    . "/nix/var/nix/profiles/default/etc/profile.d/nix-daemon.sh"
  # Official installer path
  elif [ -f "/nix/var/nix/profiles/default/etc/profile.d/nix.sh" ]; then
    . "/nix/var/nix/profiles/default/etc/profile.d/nix.sh"
  fi
fi

# Init Starship
eval "$(starship init zsh)"


# History in cache directory:
HISTFILE="$HOME/.zsh_history"
HISTSIZE=500000
SAVEHIST=500000
setopt appendhistory
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY

# Auto
if [ ! -f "/etc/NIXOS" ]; then
  source "$HOME/.nix-profile/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
  eval "$(direnv hook zsh)"
fi

# zsh-completions
if [ ! -f "/etc/NIXOS" ]; then
  FPATH="$HOME/.nix-profile/share/zsh/site-functions:$HOME/.nix-profile/share/zsh-completions:$FPATH"
  fpath=($^fpath(N)) # Remove non-existent directories

  autoload -Uz compinit
  # Get zcompdump modification day (handles macOS vs Linux stat differences)
  if [ "$(uname)" = "Darwin" ]; then
    zcompdump_day=$(stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
  else
    zcompdump_day=$(date -d @$(stat -c '%Y' ~/.zcompdump 2>/dev/null) +'%j' 2>/dev/null)
  fi
  if [ "$(date +'%j')" != "$zcompdump_day" ]; then
    compinit -u
  else
    compinit -C
  fi
fi

# History suggestions - Prefix-based history search with up/down
autoload -U up-line-or-beginning-search down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "${terminfo[kcuu1]}" up-line-or-beginning-search  # Up arrow
bindkey "${terminfo[kcud1]}" down-line-or-beginning-search  # Down arrow

# Fix key bindings
bindkey "^[[H" beginning-of-line   # Home key
bindkey "^[[F" end-of-line         # End key
bindkey "^[[3~" delete-char        # Delete key

# WSL-friendly SSH agent setup
if [ -z "$SSH_AUTH_SOCK" ] || [ ! -S "$SSH_AUTH_SOCK" ]; then
  # Kill any existing ssh-agent processes
  pkill ssh-agent 2>/dev/null
  
  # Create a fixed socket location
  export SSH_AUTH_SOCK=~/.ssh/agent.sock
  
  # Remove socket if it exists
  rm -f $SSH_AUTH_SOCK
  
  # Start a new agent with the fixed socket
  ssh-agent -a $SSH_AUTH_SOCK > /dev/null
  
  # Add keys
  find ~/.ssh -type f -not -name "*.pub" \
    -not -name "known_hosts" \
    -not -name "authorized_keys" \
    -not -name "config" \
    -exec grep -l "PRIVATE KEY" {} \; 2>/dev/null | while read key; do

      ssh-add "$key"  2>/dev/null
  done
fi